buildscript {
	ext {
		kotlinVersion = '1.0.3'
		springBootVersion = '2.0.0.BUILD-SNAPSHOT'
	}
	repositories {
		mavenCentral()
		jcenter()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath 'org.hidetake:gradle-ssh-plugin:2.7.0'
	}
}

apply plugin: 'kotlin'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.ssh'

jar {
	baseName = 'CognitiveGuru'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
	maven { url "https://jitpack.io" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
	compile('org.postgresql:postgresql')
	compile('com.github.rubenlagus:TelegramBots:v2.4.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

task buildDocker(dependsOn: build, type:Exec) {
	doFirst {
		println "Building docker image"
	}
	def dockerBuild = ["docker", "build", "-t", "cognitiveguru:latest", "."]
	commandLine dockerBuild
}

remotes {
	web01 {
		host = '54.147.152.45'
		user = 'ec2-user'
		identity = file("${System.properties['user.home']}/.ssh/apnastartup.pem")
	}
}

task deployEC2 << {
	println("Deploying to EC2")
	ssh.run {
		session(remotes.web01) {
			def remoteHome = "/home/ec2-user"

			executeSudo "yum install -y git"
			executeSudo "yum install -y java-1.8.0"
			execute(["cd", remoteHome])
			executeSudo "rm -Rf CognitiveGuru"
			execute "git clone https://github.com/SaiNageswarS/CognitiveGuru.git"

			execute "cd CognitiveGuru"
			put from: "src/main/resources/application.properties",
				into: "${remoteHome}/CognitiveGuru/src/main/resources/application.properties"

			println("Initiating Remote Build")
			execute "sh /home/ec2-user/CognitiveGuru/run.sh"
		}
	}
}